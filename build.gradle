plugins {
    id 'application'
    id 'java'
    id 'jacoco'
}

group 'uk.ashleybye'

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

repositories {
    mavenCentral()
}

dependencies {
    compile "org.openjfx:javafx-base:11"
    compile "org.openjfx:javafx-fxml:11"
    compile "org.openjfx:javafx-base:11:mac"
    compile "org.openjfx:javafx-controls:11:win"
    compile "org.openjfx:javafx-controls:11:linux"
    compile "org.openjfx:javafx-controls:11:mac"
    compile "org.openjfx:javafx-graphics:11:win"
    compile "org.openjfx:javafx-graphics:11:linux"
    compile "org.openjfx:javafx-graphics:11:mac"
    testCompile 'org.junit.jupiter:junit-jupiter-engine:5.3.1'
    testCompile "org.testfx:testfx-core:4.0.15-alpha"
    testCompile "org.testfx:testfx-junit5:4.0.15-alpha"
    testCompile "org.testfx:openjfx-monocle:jdk-11+26"
}

run {
    mainClassName = 'uk.ashleybye.tictactoe.Application'
    standardInput = System.in
}

jar {
    manifest {
        attributes(
                'Class-Path': configurations.compile.collect { it.getName() }.join(' '),
                'Main-Class': 'uk.ashleybye.tictactoe.Application'
        )
    }
    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }
    destinationDir = file("$rootDir/out")
}

wrapper {
    gradleVersion = '4.10.2'
}

clean {
    delete "${rootDir}/out"
}

test {
    useJUnitPlatform {
        excludeTags 'slow'
    }
    jvmArgs "-Dheadless=true"
}

task testSlow(type: Test) {
    useJUnitPlatform()
    jvmArgs "-Dheadless=true"
}

jacoco {
    toolVersion = "0.8.2"
}

jacocoTestReport {
    group = "Reporting"
    reports {
        xml.enabled true
        html.enabled true
    }
}

jacocoTestCoverageVerification {
    violationRules.rule {
        limit { counter = 'INSTRUCTION'; minimum = 0.90 }
    }
}

check.dependsOn jacocoTestReport
check.dependsOn jacocoTestCoverageVerification
